&НаСервере
Перем Соединение, МодульСервера, КоличествоТестов, КоличествоОшибок, КоличествоПроблем, ИмяФайлаОбработки;

&НаСервере
Перем ИдКомпоненты, АдресКомпоненты;

&НаСервере
Перем ВК, ПК, Буф, git;

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Автотестирование Тогда
		ПодключитьОбработчикОжидания("ЗавершитьРаботу", 1, Истина);
	Иначе
		Для каждого ЭлементСписка из СписокУзлов Цикл
			Элементы.Результаты.Развернуть(ЭлементСписка.Значение, Истина);
		КонецЦикла;
		СписокУзлов.Очистить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьРаботу()
	
	ЗавершитьРаботуСистемы(Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Автотестирование", Автотестирование);
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ФайлОбработки = Новый Файл(ОбработкаОбъект.ИспользуемоеИмяФайла);
	ИмяФайлаОбработки = ФайлОбработки.Имя;
	ТекущийКаталог = ФайлОбработки.Путь;
	
	ИдКомпоненты = "_" + StrReplace(New UUID, "-", "");
	МакетКомпоненты = ОбработкаОбъект.ПолучитьМакет("VanessaExt");
	АдресКомпоненты = ПоместитьВоВременноеХранилище(МакетКомпоненты, УникальныйИдентификатор);
	
	КоличествоТестов = 0;
	КоличествоОшибок = 0;
	КоличествоПроблем = 0;
	
	Попытка
		Если Автотестирование Тогда 
			МодульСервера = Вычислить("AppServer");
			Соединение = МодульСервера.СоздатьСоединение();
		КонецЕсли;
		
		ВыполнитьТесты();
		
		Если Автотестирование Тогда 
			Если КоличествоОшибок > 0 Тогда
				МодульСервера.ОтправитьСообщение(Соединение, "Имеются ошибки в тестах: " + КоличествоОшибок, "Error");
			ИначеЕсли КоличествоПроблем > 0 Тогда
				МодульСервера.ОтправитьСообщение(Соединение, "Имеются проблемы в тестах: " + КоличествоПроблем, "Warning");
			Иначе
				МодульСервера.ОтправитьСообщение(Соединение, "Тесты успешно пройдены: ", "Information");
				ЗаписьТекста = Новый ЗаписьТекста(ТекущийКаталог + "success.txt");
				ЗаписьТекста.ЗаписатьСтроку(ТекущаяУниверсальнаяДата());
				ЗаписьТекста.Закрыть();
			КонецЕсли;
		КонецЕсли;
	Исключение
		Если Автотестирование Тогда 
			Лог = Новый ЗаписьТекста(ТекущийКаталог + "autotest.log");
			Лог.ЗаписатьСтроку(ИнформацияОбОшибке().Описание);
			Лог.Закрыть();
		КонецЕсли;
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Функция ПрочитатьСтрокуJSON(ТекстJSON)
	
	Если ПустаяСтрока(ТекстJSON) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПоляДаты = Новый Массив;
	ПоляДаты.Добавить("CreationDate");
	ПоляДаты.Добавить("date");
	
	ЧтениеJSON = Новый ЧтениеJSON();
	ЧтениеJSON.УстановитьСтроку(ТекстJSON);
	Возврат ПрочитатьJSON(ЧтениеJSON, , ПоляДаты);
	
КонецФункции

&НаСервере
Функция ДобавитьГруппуТестов(Родитель, Наименование)
	
	Стр = Родитель.ПолучитьЭлементы().Добавить();
	СписокУзлов.Добавить(Стр.ПолучитьИдентификатор());
	Стр.Наименование = Наименование;
	Возврат Стр;
	
КонецФункции

&НаСервере
Процедура УстановитьФлагОшибки(ТекСтр)
	
	Если Автотестирование Тогда
		МодульСервера.ОтправитьСообщение(Соединение, ТекСтр.Наименование, "Error", ТекСтр.Результат);
	КонецЕсли;
	
	Стр = ТекСтр;
	Пока Стр <> Неопределено Цикл
		Стр.Ошибка = Истина;
		Стр = Стр.ПолучитьРодителя();
	КонецЦикла;
	
	КоличествоОшибок = КоличествоОшибок + 1;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФлагПроблемы(ТекСтр)
	
	Если Автотестирование Тогда
		МодульСервера.ОтправитьСообщение(Соединение, ТекСтр.Наименование, "Warning", ТекСтр.Результат);
	КонецЕсли;
	
	Стр = ТекСтр;
	Пока Стр <> Неопределено Цикл
		Стр.Проблема = Истина;
		Стр = Стр.ПолучитьРодителя();
	КонецЦикла;
	
	КоличествоПроблем = КоличествоПроблем + 1;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьГруппуТестов(ТекСтр)
	
	Если Автотестирование Тогда
		Статус = ?(ТекСтр.Ошибка, "Failed", ?(ТекСтр.Проблема, "Inconclusive", "Passed"));
		МодульСервера.ОтправитьТест(Соединение, ТекСтр.Наименование, ИмяФайлаОбработки, Статус);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ТестВычислить(Группа, ИмяТеста, Выражение)
	
	КоличествоТестов = КоличествоТестов + 1;
	
	Стр = Группа.ПолучитьЭлементы().Добавить();
	Стр.Наименование = ИмяТеста;
	Стр.Выражение = Выражение;
	Попытка
		Значение = Вычислить(Выражение);
		Стр.Результат = Значение;
		Стр.Подробности = Значение;
	Исключение
		Значение = Неопределено;
		Информация = ИнформацияОбОшибке();
		Стр.Результат = КраткоеПредставлениеОшибки(Информация);
		Стр.Подробности = ПодробноеПредставлениеОшибки(Информация);
		УстановитьФлагОшибки(Стр);
	КонецПопытки;
	
	Возврат Значение;
	
КонецФункции

&НаСервере
Процедура ТестВыполнить(Группа, ИмяТеста, Выражение)
	
	КоличествоТестов = КоличествоТестов + 1;
	
	Стр = Группа.ПолучитьЭлементы().Добавить();
	Стр.Наименование = ИмяТеста;
	Стр.Выражение = Выражение;
	Попытка
		Выполнить(Выражение);
	Исключение
		Значение = Неопределено;
		Информация = ИнформацияОбОшибке();
		Стр.Результат = КраткоеПредставлениеОшибки(Информация);
		Стр.Подробности = ПодробноеПредставлениеОшибки(Информация);
		УстановитьФлагОшибки(Стр);
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьТесты()
	
	Группа = ДобавитьГруппуТестов(Результаты, "Инициализация библиотеки");
	ТестВычислить(Группа, "ПодключениеБиблиотеки", "ПодключитьВнешнююКомпоненту(АдресКомпоненты, ИдКомпоненты, ТипВнешнейКомпоненты.Native)");
	ВК = ТестВычислить(Группа, "Новый:WindowsControl", "Новый(""AddIn." + ИдКомпоненты + ".WindowsControl"")");
	ПК = ТестВычислить(Группа, "Новый:ProcessControl", "Новый(""AddIn." + ИдКомпоненты + ".ProcessControl"")");
	Буф = ТестВычислить(Группа, "Новый:ClipboardControl", "Новый(""AddIn." + ИдКомпоненты + ".ClipboardControl"")");
	git = ТестВычислить(Группа, "Новый:GitFor1C", "Новый(""AddIn." + ИдКомпоненты + ".GitFor1C"")");
	ЗаписатьГруппуТестов(Группа);
	
	Группа = ДобавитьГруппуТестов(Результаты, "Свойства WindowsControl");
	ТестВычислить(Группа, "Получить:Version", "ВК.Version");
	ТестВычислить(Группа, "Получить:Версия", "ВК.Версия");
	ТестВычислить(Группа, "Получить:ВЕРСИЯ", "ВК.ВЕРСИЯ");
	ТестВычислить(Группа, "Получить:ProcessId", "ВК.ProcessId");
	ТестВычислить(Группа, "ИдентификаторПроцесса", "ВК.ИдентификаторПроцесса");
	ТестВыполнить(Группа, "Установить:ТекстБуфераОбмена", "ВК.ТекстБуфераОбмена = ""ТекстБуфераОбмена""");
	ТестВычислить(Группа, "Получить:ТекстБуфераОбмена", "ВК.ТекстБуфераОбмена");
	ЗаписатьГруппуТестов(Группа);
	
	Группа = ДобавитьГруппуТестов(Результаты, "Методы WindowsControl");
	ТестВычислить(Группа, "ПолучитьСписокОкон()", "ВК.ПолучитьСписокОкон(ВК.ИдентификаторПроцесса)");
	ЗаписатьГруппуТестов(Группа);
	
	ИмяПапки = "Autotest";
	ВременнаяПапка = ПолучитьИмяВременногоФайла("git");
	УдалитьФайлы(ВременнаяПапка);
	СоздатьКаталог(ВременнаяПапка);
	ВременнаяПапка = ВременнаяПапка + "/" + ИмяПапки + "/";
	СоздатьКаталог(ВременнаяПапка);
	
	ИмяФайла = "example.txt";
	ПолноеИмя = ВременнаяПапка + ИмяФайла;
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.ДобавитьСтроку(ЧислоПрописью(51243, "L=en_US"));
	ТекстовыйДокумент.ДобавитьСтроку(ЧислоПрописью(24565, "L=en_US"));
	ТекстовыйДокумент.Записать(ПолноеИмя, КодировкаТекста.UTF8);
	
	Группа = ДобавитьГруппуТестов(Результаты, "Тетирование GitFor1C");
	ТестВычислить(Группа, "version", "git.version");
	ТестВычислить(Группа, "init", "git.init(""" + ВременнаяПапка + """)");
	ТестВычислить(Группа, "status", "git.status()");
	ТестВычислить(Группа, "add", "git.add(""" + ИмяФайла + """)");
	ТестВычислить(Группа, "status", "git.status()");
	ЗаписатьГруппуТестов(Группа);
	
КонецПроцедуры